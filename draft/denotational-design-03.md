---
layout: post
title: "Einf√ºhrung in Denotational Design, Teil III: Einf√ºhrung in Agda"
author: markus-schlegel
tags: ["Denotational Design", "Modelling", "Formale Methoden", "Agda", "Semantik"]
---

Dieser Artikel ist der dritte einer Reihe √ºber _Denotational
Design_. In diesem Artikel lernen wir
[Agda](https://agda.readthedocs.io/en/v2.6.4.3-r1/overview.html)
kennen. Agda ist eine Programmiersprache mit einem sehr
ausdrucksstarken Typsystem. Es lassen sich mit Agda fast beliebige
formale Aussagen als Typen ausdr√ºcken. Implementierungen -- Werte
dieser Typen -- werden dadurch zu Beweisen, die zeigen, dass diese
Aussagen auch zutreffend sind. Wir legen uns im folgenden das Beispiel
mit den bin√§ren Numeralen aus dem [ersten
Artikel](https://funktionale-programmierung.de/2024/02/27/denotational-design-01.html)
vor und formalisieren es in Agda. Im n√§chsten Artikel wenden wir die
hier dargestellten Inhalte auf unser Praxisbeispiel mit den Zeitreihen
an.

<!-- more start -->

Im [ersten Artikel](https://funktionale-programmierung.de/2024/02/27/denotational-design-01.html)
dieser Reihe hatten wir die nat√ºrlichen Zahlen untersucht. Genauer:
Wir hatten gesehen, wie wir bin√§re Numerale -- reine Symbole,
zusammengesetzt aus Nullen und Einsen -- in die nat√ºrlichen Zahlen
√ºbersetzen k√∂nnen und wie wir sicherstellen k√∂nnen, dass diese
√úbersetzung vertr√§glich ist mit unserer intuitiven Vorstellung, was
denn die nat√ºrlichen Zahlen eigentlich bedeuten. Unsere √úbersetzung
hatten wir durch folgendes Gleichungssystem beschrieben:

```
ùõç(0) = 0
ùõç(1) = 1
ùõç(x0) = 2 * ùõç(x)
ùõç(x1) = 2 * ùõç(x) + 1
```

Hier sind die Zeichen `0` und `1` auf der linken Seite lediglich
Symbole ohne Bedeutung. Die Zeichen `0`, `1` und `2` auf der rechten
Seite der Gleichheitszeichen bedeuten die nat√ºrlichen Zahlen. `x0`
bzw. `x1` auf der linken Seite bedeutet, dass f√ºr ein beliebiges
Numeral `x` rechts eine Null bzw. Eins angeh√§ngt wird.

Was wir in unserem ersten Artikel dogmatisch vorausgesetzt haben --
n√§mlich die Existenz und Funktionsweise der nat√ºrlichen Zahlen --
wollen wir in diesem Artikel zun√§chst weiter formalisieren.

## Eine Formalisierung der nat√ºrlichen Zahlen

Warum wollen wir √ºberhaupt die nat√ºrlichen Zahlen weiter
formalisieren? Immerhin hat doch jeder ein ausreichendes intuitives
Verst√§ndnis vom Z√§hlen, Addieren, Multiplizieren und so weiter, oder?
Tats√§chlich k√∂nnten wir uns f√ºr die reine √úbersetzung von Numeralen in
nat√ºrliche Zahlen auf die nat√ºrlichen Zahlen in der
Agda-Standard-Library beziehen. Die Addition und Multiplikation sind
dort bereits definiert und mehr brauchen wir f√ºr die √úbersetzung gar
nicht. Sp√§ter wollen wir aber den Beweis f√ºhren, dass sich Operationen
auf den Numeralen mit denen auf den nat√ºrlichen Zahlen decken und
sp√§testens dann m√ºssen wir ein Verst√§ndnis entwickeln, wie die
nat√ºrlichen Zahlen in Agda repr√§sentiert sind. Es lohnt sich also,
dass wir diese nat√ºrlichen Zahlen selbst definieren.

Unser Vorhaben mag aussichtslos scheinen. Wir wollen die _eine
Repr√§sentation_ der nat√ºrlichen Zahlen (bin√§re Numerale) auf eine
_andere Repr√§sentation_ √ºberf√ºhren. Mehr k√∂nnen wir formal auch gar
nicht erreichen. Um √ºber die nat√ºrlichen Zahlen nachzudenken,
ben√∂tigen wir immer eine konkrete Repr√§sentation dieser. Die reinen,
abstrakten nat√ºrlichen Zahlen sind ohne konkrete Repr√§sentation gar
nicht greifbar. Man k√∂nnte sagen, dass der Begriff der nat√ºrlichen
Zahlen das Gemeinsame aller verschiedenen Repr√§sentationen ist.

Wir sind also dazu verdammt, mit konkreten Repr√§sentation der
nat√ºrlichen Zahlen zu arbeiten. Es ist damit aber nicht gleich alles
egal. Immerhin gibt es ungeschickte und geschicktere
Repr√§sentationen. Die r√∂mischen Zahlen sind eine sehr komplizierte
Repr√§sentation der nat√ºrlichen Zahlen (ohne die Null), die
Dezimalzahlen oder Bin√§rzahlen sind da schon deutlich n√ºtzlicher f√ºr
theoretische Untersuchungen. Es gibt aber noch eine weitere
Repr√§sentation, die alle anderen schl√§gt in Sachen Einfachheit. Jedes
dreij√§hrige Kind kennt diese Repr√§sentation: das Un√§rsystem.

Im Un√§rsystem wird eine Zahl einfach durch eine entsprechende Menge
von Strichen beschrieben. Eins ist I, Zwei ist II, Drei ist III, Vier
ist IIII und so weiter. Diese Bierdeckelnotation wollen wir zur
Grundlage unserer Formalisierung nehmen. Wir verwenden die
Programmiersprache Agda, um dieses Vorhaben umzusetzen. Agda ist eine
Programmiersprache mit sog. Dependent Types. Das Typsystem von Agda
l√§sst uns fast alle denkbaren Konstruktionen ausdr√ºcken --
insbesondere detaillierte Aussagen √ºber die Korrektheit unseres Codes.

In Agda f√ºhren wir einen neuen Datentyp mit `data` ein. Die Syntax ist
sehr √§hnlich zu der von
[GADTs](https://en.wikipedia.org/wiki/Generalized_algebraic_data_type)
in Haskell. Wir definieren zwei Konstruktoren: Anstatt bei der Eins
beginnen wir bei der Null und anstatt einer unendlichen Menge von
Symbolen definieren wir einen Hochz√§hl-Kombinator.

```agda
data Nat : Set where
  zero : Nat
  suc : Nat -> Nat
```

Et voila, wir haben die nat√ºrlichen Zahlen definiert. Den ersten paar
Zahlen k√∂nnen wir noch sprechendere Namen geben:

```agda
one : Nat
one = suc zero

two : Nat
two = suc one

three : Nat
three = suc two
```

Die Bedeutung dieser an sich bedeutungslosen Konstruktionen kommt erst
dann richtig zum Tragen, wenn wir ein paar Operationen auf den
nat√ºrlichen Zahlen definieren. Wir wollen beispielsweise zwei Zahlen
addieren.

```agda
+ : Nat -> Nat -> Nat
+ x y = ?
```

Wir k√∂nnen bei der Implementierung von `+` nach Konstruktionsanleitung
vorgehen. Wir schauen uns dazu an, auf welche zwei Arten das `x`
zustandegekommen sein k√∂nnte. `Nat` ist ein Summentyp aus zwei
Konstruktoren, also sollte unsere Definition auch auf diese beiden
F√§lle eingehen.

```agda
+ : Nat -> Nat -> Nat
+ zero y = ?
+ (suc x') y = ?
```

Der erste Fall `zero + y` ergibt `y`. Im zweiten Fall sollten wir
einen rekursiven Aufruf wagen.

```agda
+ : Nat -> Nat -> Nat
+ zero y = y
+ (suc x') y = suc (+ x' y)
```

Die Multiplikation ist nicht viel komplexer:

```agda
* : Nat -> Nat -> Nat
* zero m = zero
* (suc n) m = (+ m (* n m))
```

Diese drei Codebl√∂cke -- die Datendefinition, die Addition und die
Multiplikation -- k√∂nnen wir nicht weiter verifizieren. Hier m√ºssen
wir uns absolut sicher sein. Gl√ºcklicherweise ist die un√§re Kodierung
so trivial, dass uns diese Sicherheit nicht schwer f√§llt. H√§tten wir
eine bin√§re Kodierung als Grundlage der nat√ºrlichen Zahlen verwendet,
s√§he das schon anders aus.

## Die Bin√§rzahlen

Die eingangs beschriebene Definition der bin√§ren Numerale l√§sst sich
fast wortw√∂rtlich in Agda √ºbersetzen.

```agda
data Bin : Set where
  0b : Bin
  1b : Bin
  at-0 : Bin -> Bin
  at-1 : Bin -> Bin
```

Die Datendefinition der un√§ren Zahlen hatte nur zwei F√§lle; hier haben
wir vier. Allein dadurch sieht man schon, dass diese Repr√§sentation
der nat√ºrlichen Zahlen deutlich aufwendiger ist. Auch bei der
Definition einiger erster konkreter Zahlen m√ºssen wir uns ein wenig
mehr anstrengen:

```agda
10b : Bin
10b = at-0 1b

11b : Bin
11b = at-1 1b

100b : Bin
100b = at-0 (at-0 1b)
```

Im ersten Blogartikel hatten wir noch eine simple Operation auf den
bin√§ren Numeralen definiert: Das Hochz√§hlen.

```agda
inc : Bin -> Bin
inc 0b = 1b
inc 1b = 10b
inc (at-0 x) = at-1 x
inc (at-1 x) = at-0 (inc x)
```

Das ist zwar verst√§ndlicher Code, aber bei weitem nicht so trivial wie
das Hochz√§hlen in der un√§ren Repr√§sentation. Dort w√§re `+1` blo√ü ein
Synonym f√ºr den `suc`-Konstruktor.

Im ersten Blogartikel hatten wir dann bewiesen, dass `inc` auf den
bin√§ren Numeralen genau diesem `+1` auf den nat√ºrlichen Zahlen
entspricht. Dazu ben√∂tigten wir eine √úbersetzung der beiden
Welten. Diese nannten wir die Denotation oder Bedeutungsfunktion `ùõç`.

```
ùõç(0) = 0
ùõç(1) = 1
ùõç(x0) = 2 * ùõç(x)
ùõç(x1) = 2 * ùõç(x) + 1
```

Auch diese Definition k√∂nnen wir in Agda beinahe Eins-zu-Eins √ºbernehmen.[^1]

```agda
ùõç : Bin -> Nat
ùõç 0b = zero
ùõç 1b = one
ùõç (at-0 x) = (* two (ùõç x))
ùõç (at-1 x) = (+ (* two (ùõç x)) one)
```

Diese Bedeutungsfunktion ist wieder einer dieser Codebl√∂cke √ºber deren
Korrektheit wir uns schlicht sicher sein m√ºssen. Kein Computer kann
uns sagen, ob das wirklich das ist, was wir mit bin√§ren Numeralen
meinen. Erst beim n√§chsten Schritt k√∂nnen wir uns maschinelle
Untest√ºtzung erwarten.

## inc===+1

Bevor wir gleich die semantische √úbereinstimmung von `inc` und `+ one`
beweisen, basteln wir uns noch ein paar Werkzeuge, um √ºberhaupt
Aussagen √ºber unseren Code treffen zu k√∂nnen. Die einfachste Art von
Aussage, die wir treffen k√∂nnen, ist, dass zwei Codeschnipsel
"√§quivalent" sind. Da wir daran interessiert sind, dass Agda uns
statisch sagen kann, ob unser Code gleich ist, kann diese √Ñquivalenz
aber nicht die einfache Laufzeit-Gleichheit `==` wie in Haskell
sein. Nein, wir brauchen eine Gleichheit auf dem Typlevel.

Zur Erinnerung: Aussagen folgender Spielart m√∂chten wir pr√ºfen.

```
(=== (+ one two) three)
forall x y . (=== (+ x y) (+ y x))
forall x . (=== (ùõç (inc x)) (+ x one))
```

Derartige Aussagen wollen wir in Typen √ºberf√ºhren. Damit ist klar,
dass `===` ein zweistelliger Typkonstruktor sein muss. Ein erster
(scheiternder) Versuch:

```agda
data === (x : Nat) (y : Nat) : Set where
  construct : (=== x y)
```

Hier sagen wir, dass `===` ein zweistelliger Typkonstruktor ist, der
zwei Dinge `x` und `y` vom Typen `Nat` nimmt und einen Typen (`Set`)
zur√ºckgibt. Hier sehen wir zum ersten Mal den Einsatz von Dependent
Types. Das `x` k√∂nnte n√§mlich so was sein wie `zero` oder `suc zero`,
also Werte und das ist schlie√ülich auch das, was wir wollen, wenn wir
einen Typen wie `=== (+ one two) three` konstruieren:

``` agda
yes : (=== (+ one two) three)
yes = construct
```

Dieser Code geht durch den Typchecker -- aber das tut auch dieser Code:

``` agda
weird : (=== (+ one one) three)
weird = construct
```

Das ist zwar folgerichtig; es ist aber nicht das, was wir erreichen
wollten. Wir m√∂chten nur wahre Aussagen konstruieren k√∂nnen. Oder
anders ausgedr√ºckt: Die obige Definition von `===` ist noch nicht das,
was wir mit Gleichheit meinen -- wir wollen nicht nur, dass gleiche
Werte als gleich angesehen werden, sondern auch, dass ungleiche Werte
nicht als gleich angesehen werden. Wir m√∂chten also nur Werte
konstruieren k√∂nnen, die Zeuge davon sind, dass `x` und `y` auch
wirklich gleich sind. Das klingt nach einem endlosen Regress, deshalb
versuchen wir's noch mal anders: Es kann √ºberhaupt nur die Gleichheit
von Werten bezeugt werden, wenn auf beiden Seiten des
Gleichheitszeichens _derselbe_ Wert steht. Diese Gleichheit ist eine
rein reflexive Angelegenheit. Wir nennen unseren Konstruktor deshalb
`refl`. Er muss diese Form haben: `refl : (=== x x)`. Dann haut aber
die Signatur des Typkonstruktors nicht mehr hin. Wir verlagern deshalb
das zweite Argument auf die rechte Seite des Doppelpunkts und machen
damit aus dem zweistelligen Typkonstruktor einen einstelligen, der
aber eine Funktion auf dem Typlevel zur√ºckgibt.

```agda
data === (x : Nat) : Nat -> Set where
  refl : ((=== x) x)
```

Der einzige Konstruktor `refl` ist dabei gar kein Wert vom Typ `(=== x)`,
was eine Funktion w√§re, sondern die Typenfunktion wird direkt
wieder auf `x` angewendet. Der Typ `(=== x)` beschreibt die
sog. "Familie" von Beweisen, dass ein Wert gleich `x` ist. Diese
Familie ist nur "inhabited" -- hat also nur Werte -- am Typindex `x`, also es ist nur `x`
gleich `x` und der Beweis davon ist `refl`. Das hei√üt, ein Anwender
kann zwar immer noch einen Typen wie `(=== (+ one one) three)`
hinschreiben -- behaupten kann man alles --, es ist jetzt blo√ü nicht mehr
m√∂glich, auch einen Wert von diesem Typen zu erzeugen, denn `(+ one one)`
normalisiert zu `(suc (suc zero))` und `three` ist `(suc (suc (suc zero)))`.

Jetzt k√∂nnen wir endlich unser Korrektheitskriterium f√ºr `inc` ausdr√ºcken:

```agda
inc===+1 : (x : Bin) -> (=== (ùõç (inc x)) (+ (ùõç x) one))
inc===+1 = ?
```

`inc===+1` ist ein ganz gew√∂hnlicher Name. Namen in Agda k√∂nnen fast
alles sein. Wir sagen hier, dass `inc===+1` vom Typen `(x : Bin) ->
(=== (ùõç (inc x)) (+ (ùõç x) one))` ist, d.h. es ist eine Funktion von
einem beliebigen bin√§ren Numeral zu einem Beweisobjekt, das bezeugt,
dass eine Gleichheit besteht zwischen `(ùõç (inc x))` und `(+ (ùõç x)
one)`. Es gibt nur einen einzigen Konstruktor, der eine solche Gleichheit bezeugen k√∂nnte, n√§mlich `refl`. Trotzdem wird die folgende Implementierung noch nicht funktionieren.

```agda
inc===+1 : (x : Bin) -> (=== (ùõç (inc x)) (+ (ùõç x) one))
inc===+1 x = refl
```

Agda gibt diese Fehlermeldung aus:

```
ùõç (inc x) != + (ùõç x) one of type Nat
when checking that the expression refl has type
=== (ùõç (inc x)) (+ (ùõç x) one)
```

`!=` ist dabei _nicht_ die Negation unseres
`===`-Vergleichsoperators. Die Fehlermeldung sagt also nicht direkt,
dass unser Beweisunterfangen zum Scheitern verurteilt ist. Die
Fehlermeldung sagt nur, dass `refl` nicht als Beweisobjekt taugt, denn
dazu m√ºssten beide Seiten zu dem selben Term normalisieren, d.h. nach
Einsetzung aller Definitionen m√ºsste derselbe Term auf beiden Seiten
von `===` stehen. Zumindest taugt `refl` nicht _im Allgemeinen_ als
ein solches Beweisobjekt. F√ºr konkretere Aussagen klappt das schon:

```
inc10b : (=== (ùõç (inc 10b)) (+ (ùõç 10b) one))
inc10b = refl
```

Hier haben wir das `x` in `inc===+1` durch `10b` konkretisiert. Wenn
wir alle Funktionsanwendungen nach Definition aufl√∂sen (z.B. `inc 10b`
= `inc (at-0 1b)` = `at-1 1b`), erhalten wir auf beiden Seiten des
`===` das Ergebnis `suc (suc (suc zero))`. Der Konstruktor `refl` ist
also anwendbar. Das gilt auch f√ºr die ersten beiden F√§lle f√ºr `inc===+1`.

```agda
inc===+1 : (x : Bin) -> (=== (ùõç (inc x)) (+ (ùõç x) one))
inc===+1 0b = refl
inc===+1 1b = refl
inc===+1 (at-0 x) = refl
inc===+1 (at-1 x) = ?
```

√úberraschenderweise ist der dritte Fall `inc===+1 (at-0 x)` ebenfalls
unmittelbar erf√ºllbar, obwohl `x` dort als freie Variable
vorkommt. Das liegt daran, dass auch hier die Normalisierung denselben
Term liefert:

```
(ùõç (inc (at-0 x))) =
(ùõç (at-1 x)) =
(+ (* two (ùõç x)) one)
```

... auf der linken Seite und ...

```
(+ (ùõç (at-0 x)) one) =
(+ (* two (ùõç x)) one)
```

... auf der rechten Seite.

Nur im letzten Fall kann Agda den Beweis nicht automatisch f√ºhren,
weil die Normalisierung -- das Einsetzen von Definitionen -- stecken
bleibt. Wir k√∂nnen uns von Agda an der Stelle des Fragezeichens das
Beweisziel anzeigen lassen:

```
Goal: === (+ (ùõç (inc x)) (+ (ùõç (inc x)) zero))
          (+ (+ (+ (ùõç x) (+ (ùõç x) zero)) one) one)
```

Hier sehen wir die zwei steckgebliebenen Terme. Das sieht kompliziert
aus. Nachdem wir uns vom ersten Schock erholt haben, sehen wir aber,
dass dort ganz offensichtlich zu vereinfachende Terme stehen: `(+ (ùõç (inc x)) zero)`
sollte sich ja wohl zu `(ùõç (inc x))` vereinfachen
lassen. Wir erhalten dann:

```
Goal: === (+ (ùõç (inc x)) (ùõç (inc x)))
          (+ (+ (+ (ùõç x) (ùõç x)) one) one)
```

Nun machen wir hier ja Induktion, d.h. wir k√∂nnen voraussetzen, dass
die Aussage `(=== (ùõç (inc x)) (+ (ùõç x) one))` f√ºr das `x` hier schon
bewiesen ist. Diese Gleichheit k√∂nnen wir oben einsetzen und erhalten:

```
Goal: === (+ (+ (ùõç x) one) (+ (ùõç x) one))
          (+ (+ (+ (ùõç x) (ùõç x)) one) one)
```

Jetzt stehen links und rechts des `===` schon dieselben Terme mit `+`
verbunden, nur noch in unterschiedlicher Reihenfolge und
Klammerung. Wir wissen aber, dass f√ºr Addition auf den un√§ren Zahlen
das Assoziativ- und Kommutativgesetz gelten.

Als Menschen sind wir also von der Geltung unserer Behauptung
√ºberzeugt. Agda ist es noch nicht. Das liegt daran, dass die Regeln,
die wir eben angewandt haben, gar nicht bekannt sind. Die erste dieser
Regeln -- vielleicht die subtilste -- ist die, dass wir aus einer
Gleichheit von `x` und `y` und einer Gleichheit von `y` und `z` eine
Gleichheit von `x` und `z` folgern k√∂nnen -- dass `===` also transitiv
ist. Wir nennen diese Regel `trans` und anstatt sie als Axiom zu
bestimmen, beweisen wir sie:

```agda
trans : (x : Nat) -> (y : Nat) -> (z : Nat) -> (=== x y) -> (=== y z) -> (=== x z)
trans x y z refl refl = refl
```

Der Beweis ist trivial, aber `trans` ist trotzdem ein wichtiges Hilfsmittel.

Die n√§chste Regel, die wir oben verwendet haben, ist, dass `zero` ein
neutrales Element der Addition ist, also dass gilt: `(x : Nat) -> (=== (+ x zero) x)`.

```agda
zeroneutral : (x : Nat) -> (=== (+ x zero) x)
zeroneutral zero = refl
zeroneutral (suc x) = ?
```

Der Fall, dass `x` gleich `zero` ist, ist trivial bewiesen nach
Normalisierung. Der Fall, dass `x` gleich `suc x` ist, erfordert den
Beweis, dass `(suc (+ x zero))` gleich `(suc x)` ist. Wir wissen nach
Induktionsvoraussetzung, dass `(+ x zero)` gleich `x` ist, aber im
Beweisziel sind beide Seiten noch durch ein `suc` eingewickelt. Wir
brauchen eine weitere Regel, die besagt, dass aus einer Gleichheit von
`x` und `y` auch eine Gleichheit von `f x` und `f y` folgt f√ºr
beliebige Funktionen `f`. Diese Regel hei√üt Kongruenz und wir nennen
sie `cong`.

```agda
cong : (f : Nat -> Nat) -> (x : Nat) -> (y : Nat) -> (=== x y) -> (=== (f x) (f y))
cong f x y refl = refl
```

Im Beweis f√ºr `zeroneutral (suc x)` m√ºssen wir jetzt nur noch einsetzen.

```agda
zeroneutral : (x : Nat) -> (=== (+ x zero) x)
zeroneutral zero = refl
zeroneutral (suc x) = cong suc (+ x zero) x (zeroneutral x)
```

Nun fehlen noch das Assoziativgesetz ...

```agda
+assoc : (x : Nat) -> (y : Nat) -> (z : Nat) -> (=== (+ x (+ y z)) (+ (+ x y) z))
+assoc zero y z = refl
+assoc (suc x) y z = cong suc (+ x (+ y z)) (+ (+ x y) z) (+assoc x y z)
```

... und Kommutativgesetz.

```agda
+commut : (x : Nat) -> (y : Nat) -> (=== (+ x y) (+ y x))
+commut x y = ?
```

Hierbei begegnet uns allerdings noch eine Subtilit√§t. Wir machen einen
Induktionsbeweis √ºber `x`. Im `zero` Fall ist unser Beweisziel `=== y (+ y zero)`.
Das haben wir doch scheinbar schon gezeigt mit
`zeroneutral`, wenn auch anders herum. Wir brauchen also auch noch das
Kommutativgesetz f√ºr `===`. Das wird gern `sym` genannt, da es die Symmetrie von `===` zeigt.

```agda
sym : (x : Nat) -> (y : Nat) -> (=== x y) -> (=== y x)
sym x y refl = refl
```

Jetzt k√∂nnen wir die ersten F√§lle f√ºr `+commut` implementieren.

```agda
+commut : (x : Nat) -> (y : Nat) -> (=== (+ x y) (+ y x))
+commut zero y = sym (+ y zero) y (zeroneutral y)
+commut (suc x) zero = cong suc (+ x zero) x (zeroneutral x)
+commut (suc x) (suc y) = ?
```

Das Beweisziel f√ºr den zweiten Fall ist `(=== (suc (+ x y)) (+ y (suc x)))`.
Wir g√∂nnen uns noch einen letzten kleinen Helfer, der die
Vertauschbarkeit von `+` und `suc` zeigt.

```agda
+suclr : (x : Nat) -> (y : Nat) -> (=== (+ x (suc y)) (+ (suc x) y))
+suclr zero y = refl
+suclr (suc x) y = cong suc (+ x (suc y)) (+ (suc x) y) (suclr x y)
```

F√ºr den letzen Fall von `+commut` setzen wir nur noch alles zusammen.

```agda
+commut : (x : Nat) -> (y : Nat) -> (=== (+ x y) (+ y x))
+commut zero y = sym (+ y zero) y (zeroneutral y)
+commut (suc x) zero = cong suc (+ x zero) x (zeroneutral x)
+commut (suc x) (suc y) = cong
                            suc
                            (+ x (suc y))
                            (+ y (suc x))
                            (trans
                              (+ x (suc y))
                              (+ (suc x) y)
                              (+ y (suc x))
                              (+suclr x y)
                              (+commut (suc x) y))
```

## Implizite Parameter

Ay caramba! Wir haben endlich bewiesen, dass das Kommutativgesetz f√ºr
die Addition gilt. Dieser Beweis ist schon recht komplex und wir sind
noch lang nicht bei unserem eigentlichen Beweisziel `inc===+1`
angekommen. Bevor wir uns diesem widmen, refaktorisieren wir unseren
Kommutativit√§tsbeweis noch ein wenig. Bisher haben wir n√§mlich aus
didaktischen Gr√ºnden jeden Beweis bis ins kleinste von Hand selbst
durchgef√ºhrt. Agda kann aber oft selbst triviale Einsetzungen selbst
vornehmen. Den Beweis f√ºr `+commut` oben k√∂nnen wir mit sog. Holes
vereinfachen:

```agda
+commut2 : (x : Nat) -> (y : Nat) -> (=== (+ x y) (+ y x))
+commut2 zero y = sym _ _ (zeroneutral y)
+commut2 (suc x) zero = cong suc _ _ (zeroneutral x)
+commut2 (suc x) (suc y) = cong
                             suc
                             _
                             _
                             (trans
                               _
                               _
                               _
                               (+suclr _ _)
                               (+commut2 (suc x) y))
```

Ein Hole weist Agda an: F√ºll hier -- wenn m√∂glich -- die einzige
m√∂gliche L√∂sung ein. Dank Typinferenz l√§sst sich diese Vereinfachung
im Code oft anwenden. Wir k√∂nnen diese Vereinfachung sogar an der
Definitionsstelle veranlassen, indem wir manche Parameter als implizit
markieren. Dazu schreiben wir die Parameter nicht in normalen, runden
Klammern, sondern in geschweiften. Anwendungen von `trans`, `cong`,
etc. werden dadurch deutlich k√ºrzer und damit lesbarer.

```agda
trans2 : {x : Nat} -> {y : Nat} -> {z : Nat} -> (=== x y) -> (=== y z) -> (=== x z)
trans2 refl refl = refl

cong2 : {x : Nat} -> {y : Nat} -> (f : Nat -> Nat) -> (=== x y) -> (=== (f x) (f y))
cong2 f refl = refl

sym2 : {x : Nat} -> {y : Nat} -> (=== x y) -> (=== y x)
sym2 refl = refl

+commut2 : (x : Nat) -> (y : Nat) -> (=== (+ x y) (+ y x))
+commut2 zero y = sym2 (zeroneutral y)
+commut2 (suc x) zero = cong2 suc (zeroneutral x)
+commut2 (suc x) (suc y) = cong2
                             suc
                             (trans2
                               (+suclr _ _)
                               (+commut2 (suc x) y))
```

## Der finale Beweis

Etwas grunds√§tzlich neues kommt beim Beweis von `inc===+1` nicht
mehr. Wir setzen lediglich `trans`, `cong`, `sym` etc. zusammen. Der finale Beweis ist dann ein ziemliches Monstrum:

```agda
inc===+1 : (x : Bin) -> === (Œº (inc x)) (+ (Œº x) one)
inc===+1 0b = refl
inc===+1 1b = refl
inc===+1 (at-0 x) = refl
inc===+1 (at-1 x) = trans2 (+assoc (Œº (inc x)) (Œº (inc x)) zero)
                           (trans2 (zeroneutral (+ (Œº (inc x)) (Œº (inc x))))
                                   (trans2 (cong2 (Œª y ‚Üí (+ y (Œº (inc x))))
                                                  (inc===+1 x))
                                           (trans2 (cong2 (Œª y ‚Üí (+ (+ (Œº x) one) y))
                                                          (inc===+1 x))
                                                   (trans2 (+assoc (+ (Œº x) one) (Œº x) one)
                                                           (cong2 (Œª y ‚Üí (+ y one))
                                                                  (trans2 (+commut2 (+ (Œº x) one) (Œº x))
                                                                          (trans2 (+assoc (Œº x) (Œº x) one)
                                                                                  (cong2 (Œª y ‚Üí (+ y one))
                                                                                         (cong2 (Œª y ‚Üí (+ (Œº x) y))
                                                                                                (sym2 (zeroneutral (Œº x))))))))))))
```

## Zusammenfassung

Dieser letzte Beweis ist wieder recht komplex. Wir k√∂nnten diesen
Beweis noch vereinfachen und in eine deutlich verst√§ndlichere Form
bringen. Die Agda-Standardbibliothek bringt bspw. Werkzeuge f√ºr
sog. Equational-Reasoning mit. Diese Werkzeuge werden wir in einem der
n√§chsten Artikel kennen lernen.

Auch wenn der finale Beweis einigerma√üen komplex ist, h√§lt sich der
Gesamtaufwand f√ºr unser Unterfangen doch in Grenzen -- zumindest in
Anbetracht der Garantien, die wir bekommen. Die Definition der un√§ren
nat√ºrlichen Zahlen und die Definition von `===` haben wir oben nur aus
didaktischen Gr√ºnden selbst programmiert. Normalerweise w√ºrden wir
diese auch der Standardbibliothek entnehmen. Die Beweishilfsmittel,
die wir selbst definiert haben, beziehen sich alle auf das
Zusammenspiel von nat√ºrlichen Zahlen und `===`. Auch diese Hilfsmittel
sind in der Standardbibliothek bereits enthalten. Dann bleibt als
eigentlicher neuer Code nur folgendes √ºbrig:

```agda
data Bin : Set where
  0b : Bin
  1b : Bin
  at-0 : Bin -> Bin
  at-1 : Bin -> Bin

Œº : Bin -> Nat
Œº 0b = zero
Œº 1b = one
Œº (at-0 x) = (* two (Œº x))
Œº (at-1 x) = (+ (* two (Œº x)) one)

inc : Bin -> Bin
inc 0b = 1b
inc 1b = 10b
inc (at-0 x) = at-1 x
inc (at-1 x) = at-0 (inc x)

inc===+1 : (x : Bin) -> === (Œº (inc x)) (+ (Œº x) one)
inc===+1 0b = refl
inc===+1 1b = refl
inc===+1 (at-0 x) = refl
inc===+1 (at-1 x) = ...
```

Das ist doch gar nicht schlecht. Die Definitionen f√ºr `Bin` und `inc`
sind zwingend notwendig, wenn √ºberhaupt etwas laufen soll. Die
Denotation `Œº` kann man in den meisten Programmiersprachen auch
ausdr√ºcken, um dann bspw. Unit-Tests formulieren zu k√∂nnen. In Agda
gehen wir mit `inc===+1` noch einen Schritt weiter und beweisen
tats√§chlich, dass sich unser `inc` wie `+1` verh√§lt. Mehr
Verl√§sslichkeit geht nicht.

[^1]: Agda erlaubt sogar die Definition von Infix- oder Mixfix-Operatoren. Damit k√∂nnten wir die Agda-Definition noch mehr der Papier-Mathematik angleichen. Der konzeptuellen Einfachheit verzichten wir in diesem Artikel auf diese Notationswerkzeuge und verwenden eine Syntax, die eher an S-Expressions erinnert.
