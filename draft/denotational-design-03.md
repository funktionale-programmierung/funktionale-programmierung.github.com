---
layout: post
title: "EinfÃ¼hrung in Denotational Design, Teil III: EinfÃ¼hrung in Agda"
author: markus-schlegel
tags: ["Denotational Design", "Modelling", "Formale Methoden", "Agda", "Semantik"]
---

Dieser Artikel ist der dritte einer Reihe Ã¼ber _Denotational
Design_. In diesem Artikel lernen wir die [Programmiersprache
Agda](https://agda.readthedocs.io/en/v2.6.4.3-r1/overview.html)
kennen. Wir legen uns das BinÃ¤rzahlenbeispiel aus dem [ersten
Artikel](https://funktionale-programmierung.de/2024/02/27/denotational-design-01.html)
vor und formalisieren es in Agda. Im nÃ¤chsten Artikel wenden wir die
hier dargestellten Inhalte auf unser Praxisbeispiel mit den Zeitreihen
an.

<!-- more start -->

Im [ersten Artikel](https://funktionale-programmierung.de/2024/02/27/denotational-design-01.html)
dieser Reihe hatten wir die natÃ¼rlichen Zahlen untersucht. Genauer:
Wir hatten gesehen, wie wir binÃ¤re Numerale -- reine Symbole,
zusammengesetzt aus Nullen und Einsen -- in die natÃ¼rlichen Zahlen
Ã¼bersetzen kÃ¶nnen und wie wir sicherstellen kÃ¶nnen, dass diese
Ãœbersetzung vertrÃ¤glich ist mit unserer intuitiven Vorstellung, was
denn die natÃ¼rlichen Zahlen eigentlich bedeuten. Unsere Ãœbersetzung
hatten wir durch folgendes Gleichungssystem beschrieben:

```
ð›(0) = 0
ð›(1) = 1
ð›(x0) = 2 * ð›(x)
ð›(x1) = 2 * ð›(x) + 1
```

Hier sind die Zeichen `0` und `1` auf der linken Seite ledigliche
Symbole ohne Bedeutung. Die Zeichen `0`, `1` und `2` auf der rechten
Seite der Gleichheitszeichen bedeuten die natÃ¼rlichen Zahlen. `x0`
bzw. `x1` auf der linken Seite bedeutet, dass fÃ¼r ein beliebiges
Numeral `x` rechts eine Null bzw. Eins angehÃ¤ngt wird. Was wir in
unserem ersten Artikel dogmatisch vorausgesetzt haben -- die Existenz
und Funktionsweise der natÃ¼rlichen Zahlen -- wollen wir in diesem
Artikel zunÃ¤chst weiter formalisieren.

## Eine Formalisierung der natÃ¼rlichen Zahlen

Warum wollen wir Ã¼berhaupt die natÃ¼rlichen Zahlen weiter
formalisieren? Immerhin hat doch jeder ein ausreichendes intuitives
VerstÃ¤ndnis vom ZÃ¤hlen, Addieren, Multiplizieren und so weiter, oder?
TatsÃ¤chlich kÃ¶nnten wir uns fÃ¼r die reine Ãœbersetzung von Numeralen in
natÃ¼rliche Zahlen auf die natÃ¼rlichen Zahlen in der
Agda-Standard-Library beziehen. Die Addition und Multiplikation sind
dort bereits definiert und mehr brauchen wir fÃ¼r die Ãœbersetzung gar
nicht. SpÃ¤ter wollen wir aber den Beweis fÃ¼hren, dass sich Operationen
auf den Numeralen mit denen auf den natÃ¼rlichen Zahlen decken und
spÃ¤testens dann mÃ¼ssen wir ein VerstÃ¤ndnis entwickeln, wie die
natÃ¼rlichen Zahlen in Agda reprÃ¤sentiert sind. Es lohnt sich also,
dass wir diese natÃ¼rlichen Zahlen selbst definieren.

Unser Vorhaben mag aussichtslos scheinen. Wir wollen die _eine_
ReprÃ¤sentation der natÃ¼rlichen Zahlen (binÃ¤re Numerale) auf eine
andere ReprÃ¤sentation Ã¼berfÃ¼hren. Mehr kÃ¶nnen wir formal auch gar
nicht erreichen. Um Ã¼ber die natÃ¼rlichen Zahlen nachzudenken,
benÃ¶tigen wir immer eine konkrete ReprÃ¤sentation dieser. Die reinen,
abstrakten natÃ¼rlichen Zahlen sind ohne konkrete ReprÃ¤sentation gar
nicht greifbar. Man kÃ¶nnte sagen, dass der Begriff der natÃ¼rlichen
Zahlen das Gemeinsame aller verschiedenen ReprÃ¤sentationen ist.

Wir sind also dazu verdammt, mit konkreten ReprÃ¤sentation der
natÃ¼rlichen Zahlen zu arbeiten. Es ist damit aber nicht gleich alles
egal. Immerhin gibt es ungeschickte und geschicktere
ReprÃ¤sentationen. Die rÃ¶mischen Zahlen sind eine sehr komplizierte
ReprÃ¤sentation der natÃ¼rlichen Zahlen (ohne die Null), die
Dezimalzahlen oder BinÃ¤rzahlen sind da schon deutlich nÃ¼tzlicher fÃ¼r
theoretische Untersuchungen. Es gibt aber noch eine weitere
ReprÃ¤sentation, die alle anderen schlÃ¤gt in Sachen Einfachheit. Jedes
dreijÃ¤hrige Kind kennt diese ReprÃ¤sentation: das UnÃ¤rsystem.

Im UnÃ¤rsystem wird eine Zahl einfach durch eine entsprechende Menge
von Strichen beschrieben. Eins ist I, Zwei ist II, Drei ist III, Vier
ist IIII und so weiter. Diese Bierdeckelnotation wollen wir zur
Grundlage unserer Formalisierung nehmen. Wir verwenden die
Programmiersprache Agda, um dieses Vorhaben umzusetzen. Agda ist eine
Programmiersprache mit sog. Dependent Types. Das Typsystem von Agda
lÃ¤sst uns fast alle denkbaren Konstruktionen ausdrÃ¼cken -- auch
Aussagen Ã¼ber die Korrektheit unseres Codes.

In Agda fÃ¼hren wir einen neuen Datentyp Ã¤hnlich wie in Haskell mit dem
SchlÃ¼sselwort `data` ein. Wir definieren zwei Konstruktoren: Anstatt
bei der Eins beginnen wir bei der Null und anstatt einer unendlichen
Menge von Symbolen definieren wir einen HochzÃ¤hl-Kombinator.

```agda
data Nat : Set where
  zero : Nat
  suc : Nat -> Nat
```

Et voila, wir haben die natÃ¼rlichen Zahlen definiert. Den ersten paar
Zahlen kÃ¶nnen wir noch sprechendere Namen geben:

```agda
one : Nat
one = suc zero

two : Nat
two = suc two

three : Nat
three = suc two
```

Die Bedeutung dieser ansich bedeutungslosen Konstruktionen kommt erst
dann richtig zum Tragen, wenn wir ein paar Operationen auf den
natÃ¼rlichen Zahlen definieren. Wir wollen beispielsweise zwei Zahlen
addieren.

```agda
+ : Nat -> Nat -> Nat
+ x y = ?
```

Wir kÃ¶nnen bei der Implementierung von `+` nach Konstruktionsanleitung
vorgehen. Wir schauen uns dazu an, auf welche zwei Arten das `x`
zustandegekommen sein kÃ¶nnte. `Nat` ist ein Summentyp aus zwei
Konstruktoren, also sollte unsere Definition auch auf diese beiden
FÃ¤lle eingehen.

```agda
+ : Nat -> Nat -> Nat
+ zero y = ?
+ (suc x') y = ?
```

Der erste Fall `zero + y` ergibt `y`. Im zweiten Fall sollten wir
einen rekursiven Aufruf wagen.

```agda
+ : Nat -> Nat -> Nat
+ zero y = y
+ (suc x') y = suc (+ x' y)
```

Die Multiplikation ist nicht viel komplexer:

```agda
* : Nat -> Nat -> Nat
* zero m = zero
* suc n m = (+ m (* n m))
```

Diese drei CodeblÃ¶cke -- die Datendefinition, die Addition und die
Multiplikation -- kÃ¶nnen wir nicht weiter verifizieren. Hier mÃ¼ssen
wir uns absolut sicher sein. GlÃ¼cklicherweise ist die unÃ¤re Kodierung
so trivial, dass uns diese Sicherheit nicht schwer fÃ¤llt. HÃ¤tten wir
eine binÃ¤re Kodierung als Grundlage der natÃ¼rlichen Zahlen verwendet,
sÃ¤he das schon anders aus.

## Die BinÃ¤rzahlen

Die oben beschriebene Definition der binÃ¤ren Numerale lÃ¤sst sich fast wortwÃ¶rtlich in Agda Ã¼bersetzen.

```agda
data Bin : Set where
  0b : Bin
  1b : Bin
  at-0 : Bin -> Bin
  at-1 : Bin -> Bin
```

Die Datendefinition der unÃ¤ren Zahlen hatte nur zwei FÃ¤lle; hier haben
wir vier. Allein dadurch sieht man schon, dass diese ReprÃ¤sentation
der natÃ¼rlichen Zahlen deutlich aufwendiger ist. Auch bei der
Definition einiger erster konkreter Zahlen mÃ¼ssen wir uns ein wenig
mehr anstrengen:

```agda
10b : Bin
10b = at-0 1b

11b : Bin
11b = at-1 1b

100b : Bin
100b = at-0 (at-0 1b)
```

Im ersten Blogartikel hatten wir noch eine simple Operation auf den
binÃ¤ren Numeralen definiert: Das HochzÃ¤hlen.

```agda
inc : Bin -> Bin
inc 0b = 1b
inc 1b = 10b
inc (at-0 x) = at-1 x
inc (at-1 x) = at-0 (inc x)
```

Das ist zwar verstÃ¤ndlicher Code, aber bei weitem nicht so trivial wie
das HochzÃ¤hlen in der unÃ¤ren ReprÃ¤sentation. Dort wÃ¤re `+1` bloÃŸ ein
Synonym fÃ¼r den `suc`-Konstruktor.

Im ersten Blogartikel hatten wir dann bewiesen, dass `inc` auf den
binÃ¤ren Numeralen genau diesem `+1` auf den natÃ¼rlichen Zahlen
entspricht. Dazu benÃ¶tigten wir eine Ãœbersetzung der beiden
Welten. Diese nannten wir die Denotation oder Bedeutungsfunktion `ð›`.

```
ð›(0) = 0
ð›(1) = 1
ð›(x0) = 2 * ð›(x)
ð›(x1) = 2 * ð›(x) + 1
```

Auch diese Definition kÃ¶nnen wir in Agda beinahe Eins-zu-Eins Ã¼bernehmen.[^1]

```agda
ð› : Bin -> Nat
ð› 0b = zero
ð› 1b = one
ð› (at-0 x) = (* two (ð› x))
ð› (at-1 x) = (+ (* two (ð› x)) one)
```

Diese Bedeutungsfunktion ist wieder einer dieser CodeblÃ¶cke Ã¼ber deren
Korrektheit wir uns schlicht sicher sein mÃ¼ssen. Kein Computer kann
uns sagen, ob das wirklich das ist, was wir mit binÃ¤ren Numeralen
meinen. Erst beim nÃ¤chsten Schritt kÃ¶nnen wir uns maschinelle
UntestÃ¼tzung erwarten.

## inc===+1

Bevor wir gleich die semantische Ãœbereinstimmung von `inc` und `+ one`
beweisen, basteln wir uns noch ein paar Werkzeuge, um Ã¼berhaupt
Aussagen Ã¼ber unseren Code treffen zu kÃ¶nnen. Die einfachste Art von
Aussage, die wir treffen kÃ¶nnen, ist, dass zwei Codeschnipsel
"Ã¤quivalent" sind. Da wir daran interessiert sind, dass Agda uns
statisch sagen kann, ob unser Code gleich ist, kann diese Ã„quivalenz
aber nicht die einfache Laufzeit-Gleichheit `==` wie in Haskell
sein. Nein, wir brauchen eine Gleichheit auf dem Typlevel.

Zur Erinnerung: Aussagen folgender Spielart mÃ¶chten wir prÃ¼fen.

```
(=== (+ one two) three)
forall x y . (=== (+ x y) (+ y x))
forall x . (=== (ð› (inc x)) (+ x one))
```

Derartige Aussagen wollen wir in Typen Ã¼berfÃ¼hren. Damit ist klar,
dass `===` ein zweistelliger Typkonstruktor sein muss. Ein erster
(scheiternder) Versuch:

```agda
data === (x : Nat) (y : Nat) : Set where
  construct : (=== x y)
```

Hier sagen wir, dass `===` ein zweistelliger Typkonstruktor ist, der
zwei Dinge `x` und `y` vom Typen `Nat` nimmt und einen Typen (`Set`)
zurÃ¼ckgibt. Hier sehen wir zum ersten Mal den Einsatz von Dependent
Types. Das `x` kÃ¶nnte nÃ¤mlich so was sein wie `zero` oder `suc zero`,
also Werte und das ist schlieÃŸlich auch das, was wir wollen, wenn wir
einen Typen wie `one + two === three` konstruieren:

``` agda
yes : (=== (+ one two) three)
yes = construct
```

Dieser Code geht durch den Typchecker -- aber das tut auch dieser Code:

``` agda
weird : (=== (+ one one) three)
weird = construct
```

Das ist zwar folgerichtig; es ist aber nicht das, was wir erreichen
wollten. Wir mÃ¶chten nur wahre Aussagen konstruieren kÃ¶nnen. Oder
anders ausgedrÃ¼ckt: Die obige Definition von `===` ist noch nicht das,
was wir mit Gleichheit meinen -- wir wollen nicht nur, dass gleiche
Werte als gleich angesehen werden, sondern auch, dass ungleiche Werte
nicht als gleich angesehen werden. Wir mÃ¶chten also nur Werte
konstruieren kÃ¶nnen, die Zeuge davon sind, dass `x` und `y` auch
wirklich gleich sind. Das klingt nach einem endlosen Regress, deshalb
versuchen wir's noch mal anders: Es kann Ã¼berhaupt nur die Gleichheit
von Werten bezeugt werden, wenn auf beiden Seiten des
Gleichheitszeichens _derselbe_ Wert steht. Diese Gleichheit ist eine
rein reflexive Angelegenheit. Wir nennen unseren Konstruktor deshalb
`refl`. Er muss diese Form haben: `refl : (=== x x)`. Dann haut aber
die Signatur des Typkonstruktors nicht mehr hin. Wir verlagern deshalb
das zweite Argument auf die rechte Seite des Doppelpunkts und machen
damit aus dem zweistelligen Typkonstruktor einen einstelligen, der
aber eine Funktion auf dem Typlevel zurÃ¼ckgibt.

```agda
data === (x : Nat) : Nat -> Set where
  refl : ((=== x) x)
```

Der einzige Konstruktor `refl` ist dabei gar kein Wert vom Typ `(=== x)`,
was eine Funktion wÃ¤re, sondern die Typenfunktion wird direkt
wieder auf `x` angewendet. Der Typ `(=== x)` beschreibt die
sog. "Familie" von Beweisen, dass ein Wert gleich `x` ist. Diese
Familie ist nur "inhabited" -- hat also nur Werte -- am Typindex `x`, also es ist nur `x`
gleich `x` und der Beweis davon ist `refl`. Das heiÃŸt, ein Anwender
kann zwar immer noch einen Typen wie `(=== (+ one one) three)`
hinschreiben -- behaupten kann man alles --, es ist jetzt bloÃŸ nicht mehr
mÃ¶glich, auch einen Wert von diesem Typen zu erzeugen, denn `(+ one one)`
normalisiert zu `(suc (suc zero))` und `three` ist `(suc (suc (suc zero)))`.

Jetzt kÃ¶nnen wir endlich unser Korrektheitskriterium fÃ¼r `inc` ausdrÃ¼cken:

```agda
inc===+1 : (x : Bin) -> (=== (ð› (inc x)) (+ (ð› x) one))
inc===+1 = ?
```

`inc===+1` ist ein ganz gewÃ¶hnlicher Name. Namen in Agda kÃ¶nnen fast
alles sein. Wir sagen hier, dass `inc===+1` vom Typen `(x : Bin) ->
(=== (ð› (inc x)) (+ (ð› x) one))` ist, d.h. es ist eine Funktion von
einem beliebigen binÃ¤ren Numeral zu einem Beweisobjekt, das bezeugt,
dass eine Gleichheit besteht zwischen `(ð› (inc x))` und `(+ (ð› x)
one)`. Es gibt nur einen einzigen Konstruktor, der eine solche Gleichheit bezeugen kÃ¶nnte, nÃ¤mlich `refl`. Trotzdem wird die folgende Implementierung noch nicht funktionieren.

```agda
inc===+1 : (x : Bin) -> (=== (ð› (inc x)) (+ (ð› x) one))
inc===+1 = refl
```

Agda gibt diese Fehlermeldung aus:

```
ð› (inc x) != + (ð› x) one of type Nat
when checking that the expression refl has type
=== (ð› (inc x)) (+ (ð› x) one)
```

`!=` ist dabei nicht die Negation unseres
`===`-Vergleichsoperators. Die Fehlermeldung sagt also nicht direkt,
dass unser Beweisunterfangen zum Scheitern verurteilt ist. Die
Fehlermeldung sagt nur, dass `refl` nicht als Beweisobjekt taugt, denn
dazu mÃ¼ssten beide Seiten zu dem selben Wert normalisieren, d.h. dass
nach Einsetzung aller Definitionen derselbe Wert auf beiden Seiten von
`===` entsteht. Zumindest taugt `refl` nicht _im Allgemeinen_ als ein
solches Beweisobjekt. FÃ¼r konkretere Aussagen klappt das schon:

```
inc10b : (=== (ð› (inc 10b)) (+ (ð› 10b) one))
inc10b = refl
```

Hier haben wir das `x` in `inc===+1` durch `10b` konkretisiert. Wenn
wir alle Funktionsanwendungen nach Definition auflÃ¶sen (z.B. `inc 10b`
= `inc (at-0 1b)` = `at-1 1b`), erhalten wir auf beiden Seiten des
`===` das Ergebnis `suc (suc (suc zero))`. Der Konstruktor `refl` ist
also anwendbar. Das gilt auch fÃ¼r die ersten drei FÃ¤lle im allgemeinen `inc===+1`:

```agda
inc===+1 : (x : Bin) -> (=== (ð› (inc x)) (+ (ð› x) one))
inc===+1 0b = refl
inc===+1 1b = refl
inc===+1 (at-0 x) = refl
inc===+1 (at-1 x) = ?
```

Nur im letzten Fall kann Agda den Beweis nicht automatisch fÃ¼hren. Das
liegt daran, dass wir keine Informationen Ã¼ber `x` haben und deshalb
die Normalisierung -- das Einsetzen von Definitionen -- stecken
bleibt. Wir kÃ¶nnen uns von Agda an der Stelle des Fragezeichens das
Beweisziel anzeigen lassen:

```
Goal: === (+ (ð› (inc x)) (+ (ð› (inc x)) zero))
          (+ (+ (+ (ð› x) (+ (ð› x) zero)) one) one)
```

Das sieht kompliziert aus. Nachdem wir uns vom ersten Schock erholt
haben, sehen wir aber, dass dort ganz offensichtlich zu vereinfachende
Terme stehen: `(+ (ð› (inc x)) zero)` sollte sich ja wohl zu `(ð› (inc x))`
vereinfachen lassen. Wir erhalten dann:

```
Goal: === (+ (ð› (inc x)) (ð› (inc x)))
          (+ (+ (+ (ð› x) (ð› x)) one) one)
```

Nun machen wir hier ja Induktion, d.h. wir kÃ¶nnen voraussetzen, dass
die Aussage `(=== (ð› (inc x)) (+ (ð› x) one))` fÃ¼r das `x` hier schon
bewiesen ist. Diese Gleichheit kÃ¶nnen wir oben einsetzen und erhalten:

```
Goal: === (+ (+ (ð› x) one) (+ (ð› x) one))
          (+ (+ (+ (ð› x) (ð› x)) one) one)
```

Jetzt stehen links und rechts des `===` schon dieselben Terme mit `+`
verbunden, nur noch in unterschiedlicher Reihenfolge und
Klammerung. Wir wissen aber, dass fÃ¼r Addition auf den unÃ¤ren Zahlen
das Assoziativ- und Kommutativgesetz gelten.

Als Menschen sind wir also von der Geltung unserer Behauptung
Ã¼berzeugt. Agda ist es noch nicht. Das liegt daran, dass die Regeln,
die wir eben angewandt haben, gar nicht bekannt sind. Die erste dieser
Regeln -- vielleicht die subtilste -- ist die, dass wir aus einer
Gleichheit von `x` und `y` und einer Gleichheit von `y` und `z` eine
Gleichheit von `x` und `z` folgern kÃ¶nnen -- dass `===` also transitiv
ist. Wir nennen diese Regel `trans` und anstatt sie als Axiom zu
bestimmen, beweisen wir sie:

```agda
trans : (x : Nat) -> (y : Nat) -> (z : Nat) -> (=== x y) -> (=== y z) -> (=== x z)
trans x y z refl refl = refl
```

Der Beweis ist trivial, aber `trans` trotzdem ein wichtiges Hilfsmittel.

Die nÃ¤chste Regel, die wir oben verwendet haben, ist, dass `zero` ein
neutrales Element der Addition ist, also dass gilt: `(x : Nat) -> (=== (+ x zero) x)`.

```agda
zeroneutral : (x : Nat) -> (=== (+ x zero) x)
zeroneutral zero = refl
zeroneutral (suc x) = ?
```

Der Fall, dass `x` gleich `zero` ist, ist trivial bewiesen nach
Normalisierung. Der Fall, dass `x` gleich `suc x` ist, erfordert den
Beweis, dass `(suc (+ x zero))` gleich `(suc x)` ist. Wir wissen nach
Induktionsvoraussetzung, dass `(+ x zero)` gleich `x` ist, aber im
Beweisziel sind beide Seiten noch durch ein `suc` eingewickelt. Wir
brauchen eine weitere Regel, die besagt, dass aus einer Gleichheit von
`x` und `y` auch eine Gleichheit von `f x` und `f y` folgt fÃ¼r
beliebige Funktionen `f`. Diese Regel heiÃŸt Kongruenz und wir nennen
sie `cong`.

```agda
cong : (f : Nat -> Nat) -> (x : Nat) -> (y : Nat) -> (=== x y) -> (=== (f x) (f y))
cong f x y refl = refl
```

Im Beweis fÃ¼r `zeroneutral (suc x)` mÃ¼ssen wir jetzt nur noch einsetzen.

```agda
zeroneutral : (x : Nat) -> (=== (+ x zero) x)
zeroneutral zero = refl
zeroneutral (suc x) = cong suc (+ x zero) x (zeroneutral x)
```

Nun fehlen noch das Assoziativgesetz ...

```agda
+assoc : (x : Nat) -> (y : Nat) -> (z : Nat) -> (=== (+ x (+ y z)) (+ (+ x y) z))
+assoc zero y z = refl
+assoc (suc x) y z = cong suc (+ x (+ y z)) (+ (+ x y) z) (+assoc x y z)
```

... und Kommutativgesetz.

```agda
+commut : (x : Nat) -> (y : Nat) -> (=== (+ x y) (+ y x))
+commut x y = ?
```

Hierbei begegnet uns allerdings noch eine SubtilitÃ¤t. Wir machen einen
Induktionsbeweis Ã¼ber `x`. Im `zero` Fall ist unser Beweisziel `=== y (+ y zero)`.
Das haben wir doch scheinbar schon gezeigt mit
`zeroneutral`, wenn auch anders herum. Wir brauchen also auch noch das
Kommutativgesetz fÃ¼r `===`. Das wird gern `sym` genannt, da es die Symmetrie von `===` zeigt.

```agda
sym : (x : Nat) -> (y : Nat) -> (=== x y) -> (=== y x)
sym x y refl = refl
```

Jetzt kÃ¶nnen wir die ersten FÃ¤lle fÃ¼r `+commut` implementieren.

```agda
+commut : (x : Nat) -> (y : Nat) -> (=== (+ x y) (+ y x))
+commut zero y = sym (+ y zero) y (zeroneutral y)
+commut (suc x) zero = cong suc (+ x zero) x (zeroneutral x)
+commut (suc x) (suc y) = ?
```

Das Beweisziel fÃ¼r den zweiten Fall ist `(=== (suc (+ x y)) (+ y (suc x)))`.
Wir gÃ¶nnen uns noch einen letzten kleinen Helfer, der die
Vertauschbarkeit von `+` und `suc` zeigt.

```agda
+suclr : (x : Nat) -> (y : Nat) -> (=== (+ x (suc y)) (+ (suc x) y))
+suclr zero y = refl
+suclr (suc x) y = cong suc (+ x (suc y)) (+ (suc x) y) (suclr x y)
```

FÃ¼r den letzen Fall von `+commut` setzen wir nur noch alles zusammen.

```agda
+commut : (x : Nat) -> (y : Nat) -> (=== (+ x y) (+ y x))
+commut zero y = sym (+ y zero) y (zeroneutral y)
+commut (suc x) zero = cong suc (+ x zero) x (zeroneutral x)
+commut (suc x) (suc y) = cong
                            suc
                            (+ x (suc y))
                            (+ y (suc x))
                            (trans
                              (+ x (suc y))
                              (+ (suc x) y)
                              (+ y (suc x))
                              (+suclr x y)
                              (+commut (suc x) y))
```

## Implizite Parameter

Ay caramba! Wir haben endlich bewiesen, dass das Kommutativgesetz fÃ¼r
die Addition gilt. Dieser Beweis ist schon recht komplex und wir sind
noch lang nicht bei unserem eigentlichen Beweisziel `inc===+1`
angekommen. Bevor wir uns diesem widmen, refaktorisieren wir unseren
KommutativitÃ¤tsbeweis noch ein wenig. Bisher haben wir nÃ¤mlich aus
didaktischen GrÃ¼nden jeden Beweis bis ins kleinste von Hand selbst
durchgefÃ¼hrt. Agda kann aber oft selbst triviale Einsetzungen selbst
vornehmen. Den Beweis fÃ¼r `+commut` oben kÃ¶nnen wir mit sog. Holes
vereinfachen:

```agda
+commut2 : (x : Nat) -> (y : Nat) -> (=== (+ x y) (+ y x))
+commut2 zero y = sym _ _ (zeroneutral y)
+commut2 (suc x) zero = cong suc _ _ (zeroneutral x)
+commut2 (suc x) (suc y) = cong
                             suc
                             _
                             _
                             (trans
                               _
                               _
                               _
                               (+suclr _ _)
                               (+commut2 (suc x) y))
```

Ein Hole weist Agda an: FÃ¼ll hier -- wenn mÃ¶glich -- die einzige
mÃ¶gliche LÃ¶sung ein. Dank Typinferenz lÃ¤sst sich diese Vereinfachung
im Code oft anwenden. Wir kÃ¶nnen diese Vereinfachung sogar an der
Definitionsstelle veranlassen, indem wir manche Parameter als implizit
markieren. Dazu schreiben wir die Parameter nicht in normalen, runden
Klammern, sondern in geschweiften. Anwendungen von `trans`, `cong`,
etc. werden dadurch deutlich kÃ¼rzer und damit lesbarer.

```agda
trans2 : {x : Nat} -> {y : Nat} -> {z : Nat} -> (=== x y) -> (=== y z) -> (=== x z)
trans2 refl refl = refl

cong2 : {x : Nat} -> {y : Nat} -> (f : Nat -> Nat) -> (=== x y) -> (=== (f x) (f y))
cong2 f refl = refl

sym2 : {x : Nat} -> {y : Nat} -> (=== x y) -> (=== y x)
sym2 refl = refl

+commut2 : (x : Nat) -> (y : Nat) -> (=== (+ x y) (+ y x))
+commut2 zero y = sym2 (zeroneutral y)
+commut2 (suc x) zero = cong2 suc (zeroneutral x)
+commut2 (suc x) (suc y) = cong2
                             suc
                             (trans2
                               (+suclr _ _)
                               (+commut2 (suc x) y))
```

## Der finale Beweis

Etwas grundsÃ¤tzlich neues kommt beim Beweis von `inc===+1` nicht
mehr. Wir setzen lediglich `trans`, `cong`, `sym` etc. zusammen. Der finale Beweis ist dann ein ziemliches Monstrum:

```agda
inc==+1 : {x : Nml} -> Î¼(inc(x)) == ((Î¼ x) + 1n)
inc==+1 {0b} = refl
inc==+1 {1b} = refl
inc==+1 {at-0 x} = refl
inc==+1 {at-1 x} = trans (+assoc (Î¼ (inc x)) (Î¼ (inc x)) zero)
                         (trans (+identityr ((Î¼ (inc x)) + (Î¼ (inc x))))
                                (trans (cong (Î» y â†’ y + Î¼ (inc x))
                                             (inc==+1 {x}))
                                       (trans (cong (Î» y â†’ Î¼ x + 1n + y)
                                                    (inc==+1 {x}))
                                              (trans (+assoc ((Î¼ x) + 1n) (Î¼ x) 1n)
                                                     (cong (Î» y â†’ y + 1n)
                                                           (trans (+commut (Î¼ x + 1n) (Î¼ x))
                                                                  (trans (+assoc (Î¼ x) (Î¼ x) 1n)
                                                                         (cong (Î» y â†’ y + 1n)
                                                                               (cong (Î» y â†’ Î¼ x + y)
                                                                                     (sym (+identityr (Î¼ x))))))))))))
```





[^1]: Agda erlaubt sogar die Definition von Infix- oder Mixfix-Operatoren. Damit kÃ¶nnten wir die Agda-Definition noch mehr der Papier-Mathematik angleichen. Der konzeptuellen Einfachheit verzichten wir in diesem Artikel auf diese Notationswerkzeuge und verwenden eine Syntax, die eher an S-Expressions erinnert.
